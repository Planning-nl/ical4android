plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
}

def versions = [
    kotlin   : '1.9.24',
    dokka    : '1.9.20',
    ical4j   : '3.2.5',
    // latest Apache Commons versions that don't require Java 8 (Android 7)
    commonsIO: '2.6'
]

repositories {
    google()
    mavenCentral()
}

android {
    namespace 'at.bitfire.ical4android'
    compileSdk = 36

    defaultConfig {
        minSdk = 26
        targetSdk = 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "version_ical4j", "\"${versions.ical4j}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/*.md']
        }
    }
    lint {
        disable 'AllowBackup', 'InvalidPackage'
    }

    sourceSets {
        main.java.srcDirs = [ "src/main/java", "opentasks-contract/src/main/java" ]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    api("org.mnode.ical4j:ical4j:${versions.ical4j}") {
        // exclude modules which are in conflict with system libraries
        exclude group: 'commons-logging'
        exclude group: 'org.json', module: 'json'
        // exclude groovy because we don't need it
        exclude group: 'org.codehaus.groovy', module: 'groovy'
        exclude group: 'org.codehaus.groovy', module: 'groovy-dateutil'
    }
    // ical4j requires newer Apache Commons libraries, which require Java8. Force latest Java7 versions.
    // noinspection GradleDependency
    api("org.apache.commons:commons-collections4:4.2")
    // noinspection GradleDependency
    api("org.apache.commons:commons-lang3:3.8.1")

    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"

    implementation 'org.slf4j:slf4j-jdk14:2.0.3'
    implementation 'androidx.core:core-ktx:1.9.0'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'junit:junit:4.13.2'
}
